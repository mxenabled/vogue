import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
  id "com.github.mxenabled.coppuccino" version "4.4.2"
  id "groovy"
  id "java"
  id "maven-publish"
  id "java-gradle-plugin"
  id "org.jetbrains.kotlin.jvm" version "2.1.0"
}

group "com.mx.vogue"
version "1.0.3" // x-release-please-version
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
  mavenCentral()
  mavenLocal()
  gradlePluginPortal()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation "org.apache.bcel:bcel:[6.11.0,7.0[" // Security update
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.0"
  implementation "com.google.code.gson:gson:[2.0,3.0["
  //upgrade to 2.15.0 after snakeyaml2
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.3"
  implementation "com.github.ben-manes.versions:com.github.ben-manes.versions.gradle.plugin:0.53.0"
  implementation "com.github.spotbugs:spotbugs-annotations:4.9.8" // For annotating classes and methods to suppress SpotBugs violations

  api "org.mockito:mockito-inline:[4.0,5.0["
  api "org.spockframework:spock-core:2.4-M6-groovy-3.0"
}

gradlePlugin {
  plugins {
    voguePlugin {
      id = group
      implementationClass = "com.mx.vogue.VoguePlugin"
    }
    vogueJitPackPlugin {
      id = "com.github.mxenabled.vogue"
      implementationClass = "com.mx.vogue.VoguePlugin"
    }
  }
}

kotlin {
  compilerOptions {
    jvmTarget = JvmTarget.JVM_11
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "11"
  }
}

coppuccino {
  kotlin { enabled = true }
  coverage {
    minimumCoverage = 0.73
    excludes = [
      "com/mx/vogue/core/models/**"
    ]
  }
}

sourceSets {
  test {
    groovy {
      srcDirs "src/test/groovy"
    }
  }
}

test { useJUnitPlatform() }

wrapper {
  gradleVersion = "7.6.3"
  distributionType = Wrapper.DistributionType.ALL
}
