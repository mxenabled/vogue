plugins {
  id "com.github.mxenabled.coppuccino" version "1.1.1"
  id "groovy"
  id "java"
  id "maven-publish"
  id "java-gradle-plugin"
  id "com.jfrog.artifactory" version "4.28.0"
  id "org.jetbrains.kotlin.jvm" version "1.6.10"
}

group "com.mx.vogue"
version "0.5.2"
sourceCompatibility = 1.8

repositories {
  maven { url "https://artifactory.internal.mx:443/maven-local" }
  maven { url "https://artifactory.internal.mx:443/gradle-local" }
  mavenCentral()
  mavenLocal()
  gradlePluginPortal()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation "com.google.code.gson:gson:2.+"
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3"
  implementation "com.github.ben-manes.versions:com.github.ben-manes.versions.gradle.plugin:0.42.0"

  constraints {
    implementation ("com.thoughtworks.xstream:xstream:1.4.19") { because "It resolves a bajillion CVEs" }
  }

  // Unit tests
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.+"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.+"
  testImplementation "org.mockito:mockito-inline:4.+"
  testImplementation "org.spockframework:spock-core:2.2-M1-groovy-3.0"
}

gradlePlugin {
  plugins {
    voguePlugin {
      id = group
      implementationClass = "com.mx.vogue.VoguePlugin"
    }
  }
}

compileKotlin { kotlinOptions { jvmTarget = "1.8"
  } }

compileTestKotlin { kotlinOptions { jvmTarget = "1.8"
  } }

coppuccino {
  kotlin { enabled = true }
  coverage {
    minimumCoverage = 0.70
    excludes = [
      "com/mx/vogue/core/models/**"
    ]
  }
}

sourceSets {
  test { groovy { srcDirs "src/test/groovy" } }
}

test { useJUnitPlatform() }

artifactoryPublish { dependsOn jar }

artifactory {
  contextUrl = "https://artifactory.internal.mx:443"
  publish {
    repository {
      repoKey = "gradle-local"
      username = "gradle-write"
      password = System.getenv("GRADLE_ARTIFACTORY_API_KEY")
      maven = true
    }
    defaults {
      publications("mavenJava")
      invokeMethod("publications", publishing.publications.names.toArray())
    }
  }
  resolve {
    repository {
      repoKey = "maven-local"
      username = "maven-read"
      maven = true
    }
  }
}

artifactoryPublish {
  contextUrl = "https://artifactory.internal.mx:443"
  dependsOn jar
}

wrapper {
  gradleVersion = "7.4.1"
  distributionType = Wrapper.DistributionType.ALL
}
