plugins {
  id "com.github.mxenabled.coppuccino" version "3.2.1"
  id "groovy"
  id "java"
  id "maven-publish"
  id "java-gradle-plugin"
  id "org.jetbrains.kotlin.jvm" version "1.6.10"
}

group "com.mx.vogue"
version "1.0.1" // x-release-please-version
sourceCompatibility = 1.8

repositories {
  mavenCentral()
  mavenLocal()
  gradlePluginPortal()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation "org.apache.bcel:bcel:[6.6.0,7.0[" // Security update
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation "com.google.code.gson:gson:[2.0,3.0["
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3"
  implementation "com.github.ben-manes.versions:com.github.ben-manes.versions.gradle.plugin:0.42.0"
  implementation "com.github.spotbugs:spotbugs-annotations:4.7.2" // For annotating classes and methods to suppress SpotBugs violations

  constraints {
    implementation ("com.thoughtworks.xstream:xstream:1.4.19") { because "It resolves a bajillion CVEs" }
  }

  // Unit tests
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:[5.8.0,5.9.0["
  testImplementation "org.junit.jupiter:junit-jupiter-api:[5.8.0,5.9.0["
  testImplementation "org.mockito:mockito-inline:[4.0,5.0["
  testImplementation "org.spockframework:spock-core:2.2-M1-groovy-3.0"
}

gradlePlugin {
  plugins {
    voguePlugin {
      id = group
      implementationClass = "com.mx.vogue.VoguePlugin"
    }
    vogueJitPackPlugin {
      id = "com.github.mxenabled.vogue"
      implementationClass = "com.mx.vogue.VoguePlugin"
    }
  }
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  } }

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  } }

coppuccino {
  kotlin { enabled = true }
  coverage {
    minimumCoverage = 0.70
    excludes = [
      "com/mx/vogue/core/models/**"
    ]
  }
}

sourceSets {
  test { groovy { srcDirs "src/test/groovy" } }
}

test { useJUnitPlatform() }

wrapper {
  gradleVersion = "7.4.1"
  distributionType = Wrapper.DistributionType.ALL
}
